/// All stations indexed by coordinates
var stations_by_geo: nodeGeo<node<Station>>;

/// All contracts indexed by contract name
var contracts_by_name: nodeIndex<String, node<Contract>>;

type Station {
    number: int;
    position: geo;
    /// Static information about the Station
    ///
    /// We want that data to be lazily loaded, therefore we put it in a node
    detail: node<StationDetail>;
    /// Dynamic data of a station indexed by the last update timestamp
    records: nodeTime<StationRecord>;
    /// The stations Contract, this is a graph edge or an SQL relationship
    contract: node<Contract>;
    profile: node<StationProfile>;
}

type StationDetail {
    name: String;
    address: String;
    banking: bool;
    bonus: bool;
}

type StationRecord {
    status: StationStatus;
    bike_stands: int;
    available_bike_stands: int;
    available_bikes: int;
}

enum StationStatus {
    OPEN,
    CLOSED,
}

type StationProfile {
    /// `24 * 7` slots, one for each hour of the week, will help to visualize the station activity in a typical week
    hourlyProfile: GaussianProfile;
    hourlyQuantizer: MultiQuantizer;

    fn updateProfile(t: time, data: StationRecord) {
        if (data.bike_stands > 0) {
            // Convert the time to the correct slot
            var date = t.toDate(null);
            var slot = this.hourlyProfile.quantizer.quantize(Array<int> { t.dayOfWeek(null), date.hour });
            this.hourlyProfile.add(
                this.hourlyQuantizer.slot_vector(slot),
                data.available_bikes as float / data.bike_stands as float
            );
        }
    }

    fn profileToTable(): Table {
        var table = Table {};
        var endSlot = 24 * 7;
        var startSlot = 0;

        while (startSlot < endSlot) {
            var row = startSlot / 24;
            var col = startSlot % 24;
            var val = this.hourlyProfile.avg(Array<int> { row, col });

            table.set_cell(row, col, val);
            startSlot++;
        }
        return table;
    }
}

type Contract {
    name: String;
    commercial_name: String?;
    country_code: String?;
    cities: Array<String>?;

    /// list of nodes to all stations of the contract indexed by the number property
    ///
    /// This translates to a "many relationship" in SQL
    stations: nodeIndex<int, node<Station>>;
}
